buildscript {
    ext.kotlin_version = '1.3.71'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}


plugins {
    id "edu.sc.seis.macAppBundle" version "2.2.1"
    id 'edu.sc.seis.launch4j' version '2.4.4'

}

group = 'com.haleywang.putty'
version = '0.1'
sourceCompatibility = '1.8'
archivesBaseName = 'SpringRemote'

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'kotlin'


repositories {
    flatDir dirs: ['libs']
    mavenCentral()
    jcenter()
}

sourceSets {
    main.java.srcDirs += 'src/main/java'
    main.java.srcDirs += 'src/main/javaopen'
    main.kotlin.srcDirs += 'src/main/kotlin'
}

dependencies {

    compile ":eawtstub:"
    compile ":cookswing:"
    compile ":cookxml:"

    testCompile 'junit:junit:4.12'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains:annotations:13.0"
    compile 'net.java.dev.jna:jna:4.5.0'
    compile 'net.java.dev.jna:jna-platform:4.5.0'
    compile ":trove4j:"

    compile 'org.jetbrains.pty4j:pty4j:0.8.5'

    compile 'com.jcraft:jsch:0.1.54'
    compile 'com.jcraft:jzlib:1.1.3'
    compile 'com.google.guava:guava:25.1-jre'

    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.formdev:flatlaf:0.22'

    compile 'org.swinglabs:swingx:1.6.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'




}

mainClassName = "com.haleywang.putty.SpringRemoteApp"

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p['archivesBaseName'] = project.archivesBaseName.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}


macAppBundle {
    mainClassName = "com.haleywang.putty.SpringRemoteApp"
    icon = "img/SpringRemote.icns"
    bundleJRE = true
    javaProperties.put("apple.laf.useScreenMenuBar", "true")
    bundleExtras.put('NSHighResolutionCapable', true)
}


launch4j {
    mainClassName = "com.haleywang.putty.SpringRemoteApp"
    icon = "$projectDir/img/SpringRemote.ico"
}


jar {

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': 'com.haleywang.putty.SpringRemoteApp'
    }
}